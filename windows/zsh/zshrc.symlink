# ----------------------------------------------------------------------
#  All of the zsh files in this dotfiles repository will be loaded in
#  the following order:
#
#    1. topic/path.zsh
#    2. topic/*.zsh (everything else)
#    3. topic/completion.zsh
#    4. topic/plugin.zsh
#    5. topic/aliases.zsh
# ----------------------------------------------------------------------

export DOTFILES=${HOME}/.dotfiles/windows

# Get a list of all of the `.zsh` files in the repo.
typeset -U config_files
config_files=(${DOTFILES}/**/*.zsh)

# Load the path files. Any file named `path.zsh` is loaded
# first and is expected to setup `$PATH` or similar.
for file in ${(M)config_files:#*/path.zsh}; do
	source ${file}
done

# Load everything but the path, completion, plugin, and alias files.
for file in ${config_files:#*/(path|plugins|completion|aliases).zsh}; do
	source ${file}
done

# Initialize autocomplete here, otherwise the functions won't
# be loaded. Load every completion after autocomplete loads.
autoload -U compinit && compinit
for file in ${(M)config_files:#*/completion.zsh}; do
	source ${file}
done

# Load plugins here. Avoiding plugin managers because they make zsh
# sluggish with long startup load times.
for file in ${(M)config_files:#*/plugins.zsh}; do
	source ${file}
done

# Load aliases last so that they don't get overridden.
for file in ${(M)config_files:#*/aliases.zsh}; do
	source ${file}
done

unset config_files
